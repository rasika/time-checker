plugins {
	id "base"
	id "com.github.spotbugs" version "4.0.5"
	id 'net.researchgate.release' version '2.6.0'
	id 'org.springframework.boot' version '2.4.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "com.palantir.docker" version "0.22.1"
}

description = 'Time Checker - Springboot Application'

ext.timeCheckerVersion = project.version

allprojects {
	repositories {
		mavenCentral()
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	compile 'au.com.flyingkite:mobiledetect:1.1.1'
}

apply from: "gradle/javaProject.gradle"

docker {
	name "${project.name}:${project.version}"
	dockerfile file('./deployments/Dockerfile')
	files jar.archiveFile
	tag 'DockerHub', "rasika90/time-checker:${project.version}"
}

build.finalizedBy(tasks.docker)

task createK8Yaml(type: Copy) {
	from 'template/k8-deploy-template.yaml'
	rename '.*', './deployments/k8-deploy.yaml'
	destinationDir projectDir
	expand project: project
}

tasks.docker.finalizedBy(createK8Yaml)
